plugins {
    id "de.undercouch.download" version "4.0.1"
}

/**
 * Not meant for inclusion, bootstraps the gradle environment.
 * Call with ./gradlew -b gradle/bootstrap.gradle bootstrap
 */

repositories {
	mavenCentral()
	jcenter()
}

configurations {
	build
}

dependencies {
	build 'io.github.http-builder-ng:http-builder-ng-okhttp:0.17.0'
	build 'com.xlson.groovycsv:groovycsv:1.3'
}

task bootstrap(type: Copy, dependsOn: ['bootstrapDownload']) {
	from configurations.build
	into "lib"
}

task bootstrapDownload {
	doLast {
		def zipFile = 'bin/Microsoft.CodeCoverage.nupkg'
		download {
			src 'https://www.nuget.org/api/v2/package/Microsoft.CodeCoverage/16.4.0'
			dest zipFile
		}
		copy {
			from zipTree(zipFile)
			into 'bin/CodeCoverage'
			include 'build/*/CodeCoverage/**'
			eachFile {
				relativePath = RelativePath.parse(true, relativePath.segments.drop(3).join('/'))
			}
		}
		delete zipFile
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.1'
}
